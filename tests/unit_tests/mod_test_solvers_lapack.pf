module mod_test_solvers_lapack
  use mod_suite_utils
  use funit
  use mod_global_variables, only: solver, write_eigenfunctions
  use mod_solvers, only: solve_evp
  use mod_matrix_structure, only: matrix_t, new_matrix
  use mod_transform_matrix, only: array_to_matrix
  implicit none

  real(dp), parameter :: bmat_array(5, 5) = reshape( &
    [ &
      1.0d0, -2.0d0, 0.0d0, 1.0d0, 0.0d0, &
      2.0d0, 5.0d0, 4.0d0, 3.0d0, -2.0d0, &
      3.0d0, 3.0d0, 2.0d0, 0.0d0, 3.0d0, &
      4.0d0, -1.0d0, -2.0d0, 0.0d0, 0.0d0, &
      5.0d0, 0.0d0, 0.0d0, -2.0d0, -3.0d0 &
    ], &
    shape=[5, 5] &
  )
  real(dp), parameter :: bmat_array_hermitian(5, 5) = reshape( &
    [ &
      55.0d0,  13.0d0,   6.0d0,  -3.0d0, -10.0d0, &
      13.0d0,  39.0d0,  28.0d0,  13.0d0,  -1.0d0, &
      6.0d0,  28.0d0,  24.0d0,  12.0d0,  -2.0d0, &
      -3.0d0,  13.0d0,  12.0d0,  14.0d0,   0.0d0, &
      -10.0d0,  -1.0d0,  -2.0d0,   0.0d0,  22.0d0 &
    ], &
    shape=[5, 5] &
  )
  complex(dp), parameter :: amat_array(5, 5) = reshape( &
    [ &
      (1.0d0, 0.0d0), (2.0d0, 0.0d0), (3.0d0, 0.0d0), (1.0d0, 0.0d0), (0.0d0, 0.0d0), &
      (0.0d0, 2.0d0), (0.0d0, 1.0d0), (-1.0d0, 0.0d0), (0.0d0, 0.0d0), (0.0d0, -1.0d0),&
      (-3.0d0, 0.0d0), (3.0d0, 0.0d0), (0.0d0, -2.0d0), (0.0d0, 0.0d0), (0.0d0, 3.0d0),&
      (0.0d0, 0.0d0), (0.0d0, 5.0d0), (4.0d0, 0.0d0), (0.0d0, -3.0d0), (0.0d0, 2.0d0), &
      (1.0d0, 0.0d0), (0.0d0, 0.0d0), (2.0d0, 0.0d0), (0.0d0, 2.0d0), (-5.0d0, 0.0d0) &
    ], &
    shape=[5, 5] &
  )
  type(matrix_t) :: amat, bmat, bmat_hermitian, idmat
  complex(dp) :: vr(5, 5)
  complex(dp) :: eigenvals(5), expected(5)


contains

  @before
  subroutine init_test()
    vr = (0.0d0, 0.0d0)
    eigenvals = (0.0d0, 0.0d0)
    expected = (0.0d0, 0.0d0)
    write_eigenfunctions = .false.
    call set_matrices()
  end subroutine init_test


  @after
  subroutine tear_down()
    call bmat%delete_matrix()
    call bmat_hermitian%delete_matrix()
    call amat%delete_matrix()
    call idmat%delete_matrix()
  end subroutine tear_down


  subroutine set_matrices()
    integer :: i

    bmat = array_to_matrix(array=bmat_array)
    bmat_hermitian = array_to_matrix(array=bmat_array_hermitian)
    amat = array_to_matrix(array=amat_array)
    idmat = new_matrix(nb_rows=5)
    do i = 1, 5
      call idmat%add_element(row=i, column=i, element=1.0d0)
    end do
  end subroutine set_matrices


  @test
  subroutine test_unknown_solver()
    call set_name("solver (unknown)")
    solver = "unknown"
    call solve_evp(amat, bmat, eigenvals, vr)
    @assertExceptionRaised("unknown solver passed: unknown")
  end subroutine test_unknown_solver


  @test
  subroutine test_lapack_qr_invert_simple()
    call set_name("solver QR-invert (AX = wX)")
    solver = "QR-invert"
    ! these come from scipy.linalg.eigvals
    expected = [ &
      (-4.82361603918897d0, -0.55489270355141d0), &
      (-2.01155112095697d0, -2.49781686227086d0), &
      (-0.05902597975282d0, 2.062061812204889d0), &
      (0.663744370985872d0, -5.82933550828359d0), &
      (2.230448768912873d0, 2.819983261900966d0) &
    ]
    call solve_evp(amat, idmat, eigenvals, vr)
    call sort_complex_array(eigenvals)
    @assertEqual(expected, eigenvals, tolerance=TOL)
  end subroutine test_lapack_qr_invert_simple


  @test
  subroutine test_lapack_qr_invert_general()
    call set_name("solver QR-invert (AX = wBX)")
    solver = "QR-invert"
    write_eigenfunctions = .true.
    ! these come from scipy.linalg.eigvals
    expected = [ &
      (-1.0912043648361447d0, -1.471457235137944d0), &
      (-0.558926729560291d0, 0.5012768267805334d0), &
      (0.017238447154045447d0, 0.9160844663696124d0), &
      (0.6276596796722511d0, 0.48642862257574954d0), &
      (0.6282488405860116d0, -0.23788823614350615d0) &
    ]
    call solve_evp(amat, bmat, eigenvals, vr)
    call sort_complex_array(eigenvals)
    @assertEqual(expected, eigenvals, tolerance=TOL)
  end subroutine test_lapack_qr_invert_general


  @test
  subroutine test_lapack_qz_simple()
    call set_name("solver QZ-direct (AX = wX)")
    solver = "QZ-direct"
    ! these come from scipy.linalg.eigvals
    expected = [ &
      (-4.82361603918897d0, -0.55489270355141d0), &
      (-2.01155112095697d0, -2.49781686227086d0), &
      (-0.05902597975282d0, 2.062061812204889d0), &
      (0.663744370985872d0, -5.82933550828359d0), &
      (2.230448768912873d0, 2.819983261900966d0) &
    ]
    call solve_evp(amat, idmat, eigenvals, vr)
    call sort_complex_array(eigenvals)
    @assertEqual(expected, eigenvals, tolerance=TOL)
  end subroutine test_lapack_qz_simple


    @test
  subroutine test_lapack_qz_general()
    call set_name("solver QZ-direct (AX = wBX)")
    solver = "QZ-direct"
    ! these come from scipy.linalg.eigvals
    expected = [ &
      (-1.0912043648361447d0, -1.471457235137944d0), &
      (-0.558926729560291d0, 0.5012768267805334d0), &
      (0.017238447154045447d0, 0.9160844663696124d0), &
      (0.6276596796722511d0, 0.48642862257574954d0), &
      (0.6282488405860116d0, -0.23788823614350615d0) &
    ]
    call solve_evp(amat, bmat, eigenvals, vr)
    call sort_complex_array(eigenvals)
    @assertEqual(expected, eigenvals, tolerance=TOL)
  end subroutine test_lapack_qz_general


  @test
  subroutine test_lapack_qr_cholesky_simple()
    call set_name("solver QR-cholesky (AX = wX)")
    solver = "QR-cholesky"
    ! these come from scipy.linalg.eigvals
    expected = [ &
      (-4.82361603918897d0, -0.55489270355141d0), &
      (-2.01155112095697d0, -2.49781686227086d0), &
      (-0.05902597975282d0, 2.062061812204889d0), &
      (0.663744370985872d0, -5.82933550828359d0), &
      (2.230448768912873d0, 2.819983261900966d0) &
    ]
    call solve_evp(amat, idmat, eigenvals, vr)
    call sort_complex_array(eigenvals)
    @assertEqual(expected, eigenvals, tolerance=TOL)
  end subroutine test_lapack_qr_cholesky_simple



  @test
  subroutine test_lapack_qr_cholesky_general()
    call set_name("solver QR-cholesky (AX = wBX)")
    solver = "QR-cholesky"
    write_eigenfunctions = .true.
    ! these come from scipy.linalg.eigvals
    expected = [ &
      (-0.7573291032244991d0, -0.8342746803823134d0), &
      (-0.20842078110630927d0, -0.32089038501060607d0), &
      (-0.043930679967276426d0, -0.06224543824036125d0), &
      (0.010698560246044606d0, 0.3788642492245973d0), &
      (0.03473565736521634d0, 0.040781546673737325d0) &
    ]
    call solve_evp(amat, bmat_hermitian, eigenvals, vr)
    call sort_complex_array(eigenvals)
    @assertEqual(expected, eigenvals, tolerance=TOL)
  end subroutine test_lapack_qr_cholesky_general


  @test
  subroutine test_lapack_qr_cholesky_general_unequal_diags()
    call set_name("solver QR-cholesky (AX = wBX) B unequal diagonals")
    solver = "QR-cholesky"
    write_eigenfunctions = .true.
    call solve_evp(amat, bmat, eigenvals, vr)
    @assertExceptionRaised("B has unequal sub/super diagonals: 3/4")
  end subroutine test_lapack_qr_cholesky_general_unequal_diags

end module mod_test_solvers_lapack
